date-full-year        = 4DIGIT
date-month            = 2DIGIT  ; 01-12
date-m-day            = 2DIGIT  ; 01-28, 01-29, 01-30, 01-31 based on month/year
time-hour             = 2DIGIT  ; 00-23
time-minute           = 2DIGIT  ; 00-59
time-second           = 2DIGIT  ; 00-58, 00-59, 00-60 based on leap sec rules
time-sub-second       = "." 1*DIGIT
time-numeric-offset   = ("+" / "-") time-hour ":" time-minute
time-offset           = "Z" / time-numeric-offset
partial-time          = time-hour ":" time-minute ":" time-second
                        [time-sub-second]
full-date             = date-full-year "-" date-month "-" date-m-day
full-time             = partial-time time-offset
date-time             = full-date "T" full-time
