# use with https://github.com/casey/just
#
# Hermes developer convenience functions

# cspell: words prereqs, commitlog, rustls, nocapture

default:
    @just --list --unsorted

# Show the dependency tree and all enabled feature flags of every crate.
cargo-tree:
    cargo tree -e features,normal,build -f "{p}[{f}]" --workspace --frozen

# Check Dependency licenses and CVE's
license-check:
    cargo deny check --exclude-dev

# Format the rust code
code-format:
    cargo +nightly fmtfix
    cargo +nightly fmtchk

# Lint the rust code
code-lint:
    cargo lintfix -r
    cargo lint -r

# Synchronize Rust Configs
sync-cfg:
    cd .. && earthly ./hermes+sync-cfg

# Pre Push Checks
pre-push: sync-cfg code-format code-lint license-check
    # Make sure we can actually build inside Earthly which needs to happen in CI.
    cd .. && earthly ./hermes+check
    cd .. && earthly ./hermes+build

# Build Local release build of catalyst gateway
build-hermes: code-format code-lint
    cargo update
    cargo build -r 

# Run hermes
run-hermes: build-hermes
    ./target/release/hermes run
